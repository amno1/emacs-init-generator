* About
** Introduction

*BACKUP YOUR INIT FILE(S) BEFORE YOU USE THIS TOOL*

This is a small self-contained prototype of a tool to customize and generate
init file for Emacs. No external packages are needed to use this tool except
a (recent) version of Emacs itself. I have tested this with current master
(28.0.50) and Emacs 27.1 release.

To use it, open this document in Emacs, and let it evaluate the code. It will
evaluate "Init-file generator" code block. That will made four interactive
commands avialable: 

generate-init-files - generates init files from this file
install-init-files  - installs init file to user's emacs directory
install-packages    - downloads packages if they are not already installed
add-package         - adds new subheading and src code blocks for a package

You can also add new subheading manually. All packages should be under Packages
main heading. They will be ignored if placed elsewhere.

Package name should be a package name as it is in Elpa/Melpa.

There are really just two interesting macros:

(with-hook /hook-name/)

Anything in this macro will be added as a lambda to hook /hook-name/. Don't wrap
in lambdas; it is done automatically.

(with-package /package-name/)

Code in this macro will be wrapped in eval-after-load "/package-name/". Don't type
quotes, they are added automatically

There is also

(with-key-map /keymap/
  (binding . function))

This one is ment to save some typing. Map has to be present when code generated
with this macro evals, so good place to put it is in (with-hook) macro.

To remove a package from the configuration, currently you can either delete it,
comment it out or add ":disable" somewhere after the package name in the
heading. Observe that you need to re-generate the setup after any changes.

There is a small set of packages declared in this file, they are there mostly
to serve as an example. For a more worked out example, see the example-setup.org.

None of help macros are generated to init file. They are not needed for normal
Emacs operation. When you are tweaking your init file, you will anyway open this
file, so we can bring all needed macros in at that time. By doing this, the tool
is self contained and contain minimal overhead.

For more information about the tool itself, see the included Readme.org.

** References
[[https://github.com/jwiegley/use-package][Use-package]]

[[https://github.com/nilcons/emacs-use-package-fast][Emacs with use-package fast]]

[[https://lists.gnu.org/archive/html/help-gnu-emacs/2006-01/msg00021.html][Faster Emacs Startup (Emacs developer list discussion)]]

[[https://github.com/hlissner/doom-emacs/blob/develop/docs/faq.org#how-does-doom-start-up-so-quickly][Doom Emacs FAQ]]

[[https://github.com/hlissner/doom-emacs/issues/310][Why is Doom Emacs so fast? (Hlissner Github)]]

[[https://www.reddit.com/r/emacs/comments/f3ed3r/how_is_doom_emacs_so_damn_fast/][How is Doom Emacs so Fast (Reddit question)]]

[[https://two-wrongs.com/migrating-away-from-use-package][Migrating Away From Use-Package]]

[[https://nullprogram.com/blog/2017/01/30/][Writing Fast(er) Lisp]]
** Licence
Copyright (C) 2020  Arthur Miller

Author: Arthur Miller <arthur.miller@live.com>

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <https://www.gnu.org/licenses/>.
* Generator
#+NAME: onstartup
#+begin_src emacs-lisp :results output silent
(defvar init-file-bake-autoloads t)
(defvar init-file-unroll-key-bindings t)

(require 'package)
(package-quickstart-refresh)
(setq package-archives nil)

(defvar org-archive '("org" . "http://orgmode.org/elpa/"))
(defvar gnu-archive '("gnu" . "https://elpa.gnu.org/packages/"))
(defvar elpa-archive '("elpa" . "https://elpa.gnu.org/packages/"))
(defvar melpa-archive '("melpa" . "https://melpa.org/packages/"))
(defvar melpa-stable-archive '("melpa-stable" . "https://stable.melpa.org/packages/"))

(push elpa-archive package-archives)
(push melpa-archive package-archives)
(push melpa-stable-archive package-archives)
(push org-archive package-archives)
(setq package-archives (nreverse package-archives))

(setq init-file-bake-autoloads t)
(setq init-file-unroll-key-bindings t)

(unless (boundp 'init-file-hooks)
  (make-variable-buffer-local 'init-file-hooks))
(unless (boundp 'init-file-package-list)
  (make-variable-buffer-local 'init-file-package-list))

(defun on-buffer-change-hook (beg eng len)
  (setq init-file-package-list nil))

(add-hook 'after-change-functions 'on-buffer-change-hook)

(defmacro with-hook (name &rest body)
  (let (found hook)
    (setq name (if (string-match "-hook\\'" (symbol-name `,name))
		   `,name
		 (intern (concat (symbol-name name) "-hook"))))
    (setq body (nreverse body))
    (dolist (hook init-file-hooks)
      (when (equal (symbol-name (car hook)) (symbol-name name))
	(dolist (sexp (nreverse (cdr hook)))
	  (add-to-list 'body sexp))
	(setcdr hook body)
	(setq found t)))
    (unless found
      (add-to-list 'init-file-hooks (cons name body)))
    (ignore)))

(defun clean-init-file ()
  (interactive)
  (with-temp-buffer
    (insert-file-contents "init.el")
    (beginning-of-buffer)
    (while (not (eobp))
      (when (char-equal (char-after) ?\^M)
	(delete-char 1))
      (forward-char 1))
    (write-region (point-min) (point-max) "init.el")))

(defmacro with-package (name &rest body)
  (let ((package (symbol-name name)))
    (append-to-init-file "(with-eval-after-load ")
    (append-to-init-file (concat "'" package "\n "))
    (emit-to-init-file body)
    (append-to-init-file ")\n")))

(defmacro with-key-map (mapname &rest body)
  `(let ((map (eval-and-compile (if (string-match-p "-map$" (symbol-name ',mapname))
                                    (symbol-name ',mapname)
                                  (concat (symbol-name ',mapname) "-map"))))
	 (defs '(,@body)))
     (dolist (def defs)
       (define-key (symbol-value (intern map))
	 (if (vectorp (car def)) (car def)
	   (read-kbd-macro (car def))) (cdr def)))))

(defun emit-keymap (map)
  (let (key
        (body (cdr map))
        (mapname (if (string-match-p "-map$" (symbol-name (car map)))
                     (symbol-name (car map))
                   (concat (symbol-name (car map)) "-map"))))
    (dolist (stm `,@body)
      (if (vectorp (eval (car stm)))
	  (setq key (prin1-to-string (car stm)))
	(setq key (concat "(kbd \"" (car stm) "\")")))
      (append-to-init-file
       (concat "(define-key "
               mapname
	       key " '" (prin1-to-string (cdr stm))
	       ")")))))

(defun append-to-init-file (data &optional file)
  (unless file (setq file "init.el"))
  (let (insert-point)
    (cond ((stringp data)
	   (write-region data nil file t 0))
	  ((bufferp data)
	   (with-current-buffer data
	     (write-region (point-min) (point-max) file t 0))))))

(defun emit-sexp-to-init-file (sexp)
  (if init-file-unroll-key-bindings
      (if (equal (symbol-name (car sexp)) "with-key-map")
	  (emit-keymap (cdr sexp))
	(append-to-init-file (prin1-to-string sexp)))
    (append-to-init-file (prin1-to-string sexp))))

(defun emit-to-init-file (&rest body)
  (dolist (sexp body)
    (if (listp (car sexp))
	(dolist (s sexp)
	  (emit-sexp-to-init-file s))
      (emit-sexp-to-init-file sexp))))

(defun emit-packages ()
  (dolist (p (get-package-list))
    (message "Emiting package: %s" (aref p 0))
    (when (package-enabled-p p)
      (eval-region (aref p 4) (aref p 5)))))

(defun emit-hooks ()
  (message "Emiting hooks.")
  (dolist (hook init-file-hooks)
    (message "Hook: %s" (car hook))
    (if (equal (symbol-name (car hook)) "early-init-hook")
	  (dolist (elt (nreverse (cdr hook)))
	    (append-to-init-file (prin1-to-string elt) "early-init.el"))
      (progn
	  (append-to-init-file (concat "(add-hook '"
			               (symbol-name (car hook))
			               " #'(lambda nil\n"))
          (dolist (s (cdr hook))
              (emit-to-init-file s))
	  (append-to-init-file "))")))))

(defun get-load-paths()
  (let ((elpa (expand-file-name "elpa" user-emacs-directory))
	(archives (expand-file-name "elpa/archives" user-emacs-directory)) 
	package-paths)
    (dolist (path (directory-files elpa t directory-files-no-dot-files-regexp))
      (when (file-directory-p path)
	(unless (string= path archives)
	  (push path package-paths))))
    package-paths))

(defun emit-load-paths ()
  (message "Emitting load-path")
    (append-to-init-file (concat "\n(nconc load-path '"
		    (prin1-to-string (get-load-paths))
		    ")\n") "early-init.el"))

(defun emit-autoloads-file ()
  (message "Emiting autoloads")
  (let ((al "autoloads.el")
	(pq (expand-file-name "package-quickstart.el"
			      user-emacs-directory)))
    (unless (file-exists-p pq)
      (package-quickstart-refresh))
    (with-temp-file al
      (insert-file-contents pq)
      (goto-char (point-min))
      (kill-line 2)
      (goto-char (point-max))
      (kill-line -4)
      (goto-char (point-min))
      (while (not (eobp))
        (when (re-search-forward "^(add-to-list" (line-end-position) t)
          (beginning-of-line)
          (kill-line 2))
        (beginning-of-line)
        (forward-line 1)))))

(defmacro maybe-remove-file (file)
  `(let ((f ,file))
     (when (file-exists-p f)
       (delete-file f)
       (message "Removed file %s" f))))

(defun tangle-init-file (&optional file)
  (message "Exporting init files.")
  (unless file
    (setq file "init.el"))
  (maybe-remove-file "init.el")
  (maybe-remove-file "init.elc")
  (maybe-remove-file "early-init.el")
  (with-temp-file "init.el"
    (insert ";;; init.el -*- lexical-binding: t; -*-\n")
    (insert ";;; This file is machine generated by init-file generator, don't edit\n")
    (insert ";;; manually, edit instead file init.org and generate new init file from it.\n\n"))
  (with-temp-file "early-init.el"
    (insert ";;; early-init.el -*- lexical-binding: t; -*-\n")
    (insert ";;; This file is machine generated by init-file generator, don't edit\n")
    (insert ";;; manually, edit instead file init.org and generate new init file from it.\n\n"))
  (setq init-file-hooks nil)
  (emit-autoloads-file)
  ;; are we baking quickstart file?
  (when init-file-bake-autoloads
    ;;(append-to-init-file "\n(setq package-quickstart nil package-enable-at-startup nil package--init-file-ensured t)\n" "early-init.el")
    ;;(emit-load-paths)
    ;;(append-to-init-file "(defvar package-activated-list nil)")
    (with-temp-buffer
      (insert-file-contents-literally "autoloads.el")
      (append-to-init-file (current-buffer)))
    ;;(append-to-init-file "(package-activate-all)")
    )
  ;; generate stuff
  (emit-packages)
  ;; do this after user init stuff
  (with-hook after-init
	     (setq gc-cons-threshold       16777216
		   gc-cons-percentage      0.1
		   file-name-handler-alist old-file-name-handler))
  (emit-hooks) ;; must be done after emiting packages
  ;; fix init.el
  (append-to-init-file "\n;; Local Variables:\n")
  (append-to-init-file ";; byte-compile-warnings: (not free-vars unresolved))\n")
  (append-to-init-file ";; End:\n")
  ;;(clean-init-file)
  )

(defun goto-code-start (section)
  (goto-char (point-min))
  (re-search-forward section)
  (re-search-forward "begin_src.*emacs-lisp")
  (skip-chars-forward "\s\t\n\r"))

(defun goto-code-end ()
  (re-search-forward "end_src")
  (beginning-of-line))

(defun generate-init-files ()
  (interactive)
  (message "Exporting init.el ...")
  (tangle-init-file)
  (let ((tangled-file "init.el"))
    ;; always produce elc file
    (byte-compile-file tangled-file)
    (when (featurep 'nativecomp)
      (message "Native compiled %s" (native-compile tangled-file)))
    (message "Tangled and compiled %s" tangled-file))
  (message "Done."))

(defun install-file (file)
  (when (file-exists-p file)
    (unless (equal (file-name-directory buffer-file-name)
		   (expand-file-name user-emacs-directory))
      (copy-file file user-emacs-directory t))
    (message "Wrote: %s." file)))

(defun install-init-files ()
  (interactive)
  (let ((i "init.el")
	(ic "init.elc")
	(ei "early-init.el")
	(al "autoloads.el")
	(pq (expand-file-name "package-quickstart.el" user-emacs-directory))
	(pqc (expand-file-name "package-quickstart.elc" user-emacs-directory)))
    (install-file i)
    (install-file ei)
    (unless (file-exists-p ic)
      (byte-compile (expand-file-name el)))
    (install-file ic)
    (unless init-file-bake-autoloads
      (byte-compile pq))
    (when init-file-bake-autoloads
      ;; remove package-quickstart files from .emacs.d
      (when (file-exists-p pq)
	(delete-file pq))
      (when (file-exists-p pqc)
	(delete-file pqc)))))

(defun get-package-list ()
  (when (buffer-modified-p)
    (setq init-file-package-list nil))
  (unless init-file-package-list
    (save-excursion
      (goto-char (point-min))
      (let ((bound (re-search-forward "^\\* Epilog"))
	    package packages start end)
	(goto-char (point-min))
	(re-search-forward "^\\* Packages")
	(while (re-search-forward "^\\*\\* " bound t)
	  (setq package (vector nil t t "" 0 0)
		start (point) end (line-end-position))
	  ;; package name
	  (when (re-search-forward ":" end t)
	    (setq end (point)))
	  (goto-char end)
	  (skip-chars-backward ":\s\t\r\n")
	  (aset package 0 (intern (buffer-substring-no-properties
				   start (point))))
	  (goto-char start)
	  ;; enabled?
	  (when (search-forward ":disable" (line-end-position) t)
	    (aset package 1 nil))
	  (goto-char start)
	  ;; installable?
	  (when (search-forward ":pseudo" (line-end-position) t)
	    (aset package 2 nil))
	  (goto-char start)
	  ;; pinned to repository?
	  (dolist (repo package-archives)
	    (when (re-search-forward (concat ":" (car repo)) (line-end-position) t)
	      (aset package 3 (car repo))))
	  ;; code start
	  (re-search-forward "begin_src.*emacs-lisp" bound t)
	  (aset package 4 (point))
	  (re-search-forward "end_src$" bound t)
	  (beginning-of-line)
	  (aset package 5 (point))
	  (push package init-file-package-list)
	  (setq init-file-package-list (nreverse init-file-package-list))))))
  init-file-package-list)

;; Install packages
(defun ensure-package (package)
  (let ((p (aref package 0)))
    (unless (package-installed-p p)
      (message "Installing package: %s" p)
      (package-install p))))

(defun package-pseudo-p (package)
  (not (aref package 2)))

(defun package-enabled-p (package)
  (aref package 1))

(defun package-installable-p (package)
  (and (aref package 1) (aref package 2)))

(defun install-packages (&optional packages)
  (interactive)
  (package-initialize)
  (package-refresh-contents)
  (unless packages
    (setq packages (get-package-list)))
  (dolist (p packages)
    (when (package-installable-p p)
      (ensure-package p))))

;; help fns to work with init.org
(defun add-package (package)
  ""
  (interactive "sPackage name: ")
  (goto-char (point-min))
  (when (re-search-forward "^* Packages")
    (forward-line 1)
    (insert (concat "** " package
		    "\n#+begin_src emacs-lisp\n"
		    "\n#+end_src\n"))
    (forward-line -2)))

(defun add-pseudo-package (package)
  ""
  (interactive "sPackage name: ")
  (goto-char (point-min))
  (when (re-search-forward "^* Packages")
    (forward-line 1)
    (insert (concat "** " package "\t\t:pseudo"
		    "\n#+begin_src emacs-lisp\n"
		    "\n#+end_src\n"))
    (forward-line -2)))

(defun install-and-configure ()
  (interactive)
  (install-packages)
  (generate-init-files)
  (install-init-files))

(defun configure-emacs ()
  (interactive)
  (generate-init-files)
  (install-init-files))
#+end_src
* Packages
** early-init :pseudo
#+begin_src emacs-lisp
(with-hook early-init
           (defvar old-file-name-handler file-name-handler-alist)
           (setq file-name-handler-alist nil)
           
           (setq gc-cons-threshold most-positive-fixnum
                 load-prefer-newer t
                 package-quickstart t
                 package-enable-at-startup nil
                 package--init-file-ensured t))
#+end_src
** async
#+begin_src emacs-lisp
(with-package async
              (autoload 'dired-async-mode "dired-async.el" nil t)
              (async-bytecomp-package-mode 1)
              (diminish 'async-dired-mode))
#+end_src
** auto-package-update
#+begin_src emacs-lisp
(with-hook auto-package-update-after
           (message "Refresh autoloads")
           (package-quickstart-refresh))

(with-package auto-package-update
              (setq auto-package-update-delete-old-versions t
                    auto-package-update-interval nil))
#+end_src
** beacon
#+begin_src emacs-lisp
(with-hook after-init
           (beacon-mode t)
	   (diminish 'beacon-mode)
#+end_src
** diminish
#+begin_src emacs-lisp

#+end_src
** expand-region
#+begin_src emacs-lisp
(with-hook expand-region-mode
           (diminish 'expand-region-mode))
#+end_src
** helpful
#+begin_src emacs-lisp
  (with-hook after-init
    (with-key-map global
      ("<f1>"  . helpful-variable)
      ("C-h v" . helpful-variable)
      ("C-h k" . helpful-key)
      ("C-h f" . helpful-callable)
      ("C-h j" . helpful-at-point)
      ("C-h u" . helpful-command)))
#+end_src
** org                                                                      :pseudo
#+begin_src emacs-lisp
  (with-package org

   (defun get-html-title-from-url (url)
     "Return content in <title> tag."
     (require 'mm-url)
     (let (x1 x2 (download-buffer (url-retrieve-synchronously url)))
       (with-current-buffer download-buffer
	 (goto-char (point-min))
	 (setq x1 (search-forward "<title>"))
	 (search-forward "</title>")
	 (setq x2 (search-backward "<"))
	 (mm-url-decode-entities-string (buffer-substring-no-properties x1 x2)))))

   (defun my-org-insert-link ()
     "Insert org link where default description is set to html title."
     (interactive)
     (let* ((url (read-string "URL: "))
	    (title (get-html-title-from-url url)))
       (org-insert-link nil url title)))

  (setq  org-log-done 'time
	 org-todo-keywords '((sequence "TODO" "INPROGRESS" "DONE"))
	 org-todo-keyword-faces '(("INPROGRESS" . (:foreground "blue" :weight bold)))
	 org-directory (expand-file-name "~/Documents/")
	 org-default-notes-file (expand-file-name "notes.org" org-directory)
	 org-use-speed-commands       t
	 org-src-preserve-indentation t
	 org-export-html-postamble    nil
	 org-hide-leading-stars       t
	 org-make-link-description    t
	 org-hide-emphasis-markers    t
	 org-startup-folded           'overview
	 org-startup-indented         t))
#+end_src
** which-key
#+begin_src emacs-lisp
(with-hook after-init
  (which-key-mode t)
  (diminish 'which-key-mode))
#+end_src
** wrap-region
#+begin_src emacs-lisp
(with-hook after-init
  (wrap-region-global-mode t)
  (diminish 'wrap-region-mode))
#+end_src
* Epilog
#+begin_src emacs-lisp  
;; Local Variables:
;; byte-compile-warnings: (not free-vars unresolved))
;; eval: (progn (org-babel-goto-named-src-block "onstartup") (org-babel-execute-src-block) (outline-hide-sublevels 2))
;; End:
#+end_src

